apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "uiuc.bioassay.elisa"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.main.jni.srcDirs = []
    sourceSets.main.assets.srcDirs = ['src/main/assets']

    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory.getAbsolutePath()
        commandLine ndkDir + "/ndk-build.cmd",
                'NDK_PROJECT_PATH=build/intermediates/ndk',
                'NDK_LIBS_OUT=src/main/jniLibs',
                'APP_BUILD_SCRIPT=src/main/jni/Android.mk',
                'NDK_APPLICATION_MK=src/main/jni/Application.mk'
    }

    task ndkClean(type: Exec, description: 'Clean JNI via NDK') {
        def ndkDir = android.ndkDirectory.getAbsolutePath()
        commandLine ndkDir + "/ndk-build.cmd",
                'clean',
                'NDK_PROJECT_PATH=build/intermediates/ndk',
                'NDK_LIBS_OUT=src/main/jniLibs',
                'APP_BUILD_SCRIPT=src/main/jni/Android.mk',
                'NDK_APPLICATION_MK=src/main/jni/Application.mk'
    }



    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    packagingOptions {
        exclude    'META-INF/services/javax.annotation.processing.Processor'
        pickFirst  'META-INF/maven/org.bytedeco.javacpp-presets/opencv/pom.properties'
        pickFirst  'META-INF/maven/org.bytedeco.javacpp-presets/opencv/pom.xml'
        pickFirst  'META-INF/maven/org.bytedeco.javacpp-presets/ffmpeg/pom.properties'
        pickFirst  'META-INF/maven/org.bytedeco.javacpp-presets/ffmpeg/pom.xml'
    }

    lintOptions {
        abortOnError false
    }

    clean.dependsOn 'ndkClean'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.google.android.gms:play-services:8.3.0'
    compile files('libs/dropbox-android-sdk-1.6.3.jar')
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.3'
    compile 'com.android.support:multidex:1.0.0'
    compile 'org.bytedeco:javacv:1.2'
    compile 'org.bytedeco:javacpp:1.2.4'
    compile 'org.bytedeco.javacpp-presets:opencv:3.1.0-0.12'
    compile 'org.bytedeco.javacpp-presets:ffmpeg:3.1.2-1.2'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    compile group: 'net.sf.opencsv', name: 'opencsv', version: '2.3'
}

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

configurations {
    all*.exclude group: 'org.bytedeco', module: 'javacpp-presets'
}
